' It's a Republican Wasp!
' Similar to the Republican Bee
' Swarm 1Alpha
' By Commander Keen
' 25th May 2008

def badarea 51

cond
1 1 =
start
*.robage 0 =
140 .eye6width store
280 .eye7width store
280 .eye8width store
280 .eye9width store
-68 .eye1dir store
-68 .eye2dir store
-68 .eye3dir store
-68 .eye4dir store
-68 .eye5dir store
-140 .eye6dir store
317 .eye7dir store
634 .eye8dir store
970 .eye9dir store
-2 .focuseye store
.shoot .ploc store
.tie inc
64 .badarea store
clearbool
1 1 =
*.robage .tout1 store
50 *.poison sub 0 floor .strpoison store
50 *.shell sub 0 floor .mkshell store
*.nrg *.body 10 mult add 20 div *.body sub 1 floor .strbody store
*.body *.nrg *.body 10 mult add 20 div sub 0 floor 30000 *.nrg sub 10 div ceil  .fdbody store
*.tiepres .tienum store
0 .shootval store
31999 .sharenrg store
clearbool
*.tin1 *.trefage !=
*.trefshoot 0 !=
.shootval .tieloc store
-31999 .tieval store
clearbool
*.tin1 *.trefage !=
*.treftie 0 !=
.tieval .tieloc store
31999 .tieval store
clearbool
*.tin1 *.trefage !=
*.treftie 0 =
*.trefshoot 0 =
-1 .tieloc store
-31999 .tieval store
clearbool
*.trefage *.tin1 =
*.tiepres .deltie store
clearbool
stop

cond
*.eye1 *.eye2 *.eye3 *.eye4 *.eye5 *.eye6 *.eye7 *.eye8 *.eye9 add add add add add add add add 0 =
*.myaimdx *.refaimdx = or
*.badarea 16 >=
start
1 *.timer 8 mod sgn sub 10 mult .up store
1 *.timer 2 add 8 mod sgn sub 10 mult .dx store
1 *.timer 4 add 8 mod sgn sub 10 mult .dn store
1 *.timer 6 add 8 mod sgn sub 10 mult .sx store
stop

cond
*.eye1 *.eye2 *.eye3 *.eye4 *.eye5 add add add add 0 =
*.myaimdx *.refaimdx = or
start
157 .aimdx store
.badarea inc
stop

cond
*.eye3 0 =
*.eye4 0 !=
start
34 .aimdx store
stop
cond
*.eye3 0 =
*.eye2 0 !=
start
-34 .aimdx store
stop
cond
*.eye3 0 =
*.eye5 0 !=
start
68 .aimdx store
stop
cond
*.eye3 0 =
*.eye1 0 !=
start
-68 .aimdx store
stop

cond
*.eyef 0 !=
*.myaimdx *.refaimdx !=
start
'1 *.refshell sgn sub -5 mult 1 sub .shoot store
-6 5 *.refshell sgn mult add *.refshell *.refpoison mult sgn 5 mult sub .shoot store
*.velsx .dx store
128 *.eyef sub *.velup sub *.veldn 2 div floor *.refvelup add .up store
*.refxpos *.refypos angle .setaim store
0 .badarea store
.tie inc
stop

cond
*.nrg *.body 10 mult add 1000 >
start
50 .repro store
stop

cond
*.nrg *.body 10 mult add *.totalmyspecies 1000 mult 10000 add 1000 100 mult ceil <
*.badarea 64 >=
start
*.timer 4 add 8 mod 0 =
*.maxvel *.veldn sub .dn store
*.timer 8 mod 0 =
0 *.maxvel *.velup sub sub .dn store
clearbool
stop